(define-module (stones player)
  (import (mesche math)
          (substratic input)
          (substratic renderer)
          (stones input)
          (stones screen)))

;; States
;; - Standing
;; - Running
;; - Jumping

(define gravity-accel-y 9)
(define damping 3)

(define player-size 32)
(define player-color (rgb 198 40 242))
(define player-jump-vel-y -4)
(define player-move-accel-x 7)
(define player-max-vel-x 25)
(define player-max-vel-y 7)
(define player-controller (create-controller))

(define-record-type player
  (fields x
          y
          vel-x
          vel-y
          accel-x
          accel-y
          state))

(define (create-player) :export
  (make-player :x 100
               :y 500
               :vel-x 0
               :vel-y 0
               :accel-x 0
               :accel-y 0
               :state 'standing))

(define (handle-player-input player input-event) :export
  (handle-controller-input player-controller input-event))

(define (read-controller player controller time-delta)
  ;; Look at controller state to change velocity
  (let ((neg-move (* -1 player-move-accel-x)))
    ;; (if (controller-up controller)
    ;;     (player-vel-y-set! player neg-move)
    ;;     (if (controller-down controller)
    ;;         (player-vel-y-set! player player-move-speed)
    ;;         (player-vel-y-set! player 0)))
    (if (controller-left controller)
        (player-accel-x-set! player neg-move)
        (if (controller-right controller)
            (player-accel-x-set! player player-move-accel-x)
            (player-accel-x-set! player 0)))
    (if (controller-jump controller)
        ;; TODO: Add jump impulse
        (if (not (equal? (player-state player) 'jumping))
            (begin
              (player-vel-y-set! player player-jump-vel-y)
              (player-state-set! player 'jumping))))))

(define (check-moving-collision screen player left-x right-x vel-x)
  (let ((top-y (floor (/ (player-y player) tile-size)))
        (bottom-y (floor (/ (+ (player-y player)
                               (- player-size 1))
                            tile-size))))

    ;; Check for collision against a wall tile
    (if (or (and (< vel-x 0)
                 (screen-tile-wall? screen left-x bottom-y))
            (and (> vel-x 0)
                 (screen-tile-wall? screen right-x bottom-y)))
        ;; Stop horizontal movement
        (begin
          (player-x-set! player (* (if (< vel-x 0)
                                       (+ left-x 1)
                                       (- right-x 1))
                                   tile-size))
          (player-accel-x-set! player 0)
          (player-vel-x-set! player 0)))))

;; Looking at tile position only
;; movement on X, top tile bottom tile, they could be same if the player fits within the tile
;; movement on Y, left tile right tile, they could same

(define (check-falling-collision screen player left-x right-x top-y bottom-y)
  ;; When not jumping, check for collision with ground
  ;; so that the jumping/falling state can be enabled.
  ;; If jumping, check for collision with ground to see
  ;; if we should stop falling

  (let ((left-x (floor (/ (+ (player-x player) 3)
                          tile-size)))
        (right-x (floor (/ (+ (player-x player)
                              (- player-size 3))
                           tile-size))))
    ;; Check for collision against a ground tile
    (if (or (screen-tile-ground? screen left-x bottom-y)
            (and (not (equal? left-x right-x))
                 (screen-tile-ground? screen right-x bottom-y)))
        (if (equal? (player-state player) 'jumping)
            (begin
              ;; Stop the player from moving
              (player-y-set! player (* top-y tile-size))
              (player-vel-y-set! player 0)
              (player-state-set! player 'standing)))
        (begin
          (player-state-set! player 'jumping)))))

(define (debug label value)
  (display label)
  (display ": ")
  (display value)
  (display "\n")
  value)

(define (update-player player screen game-event-handler time-delta) :export
  ;; Read the player input and decide how to handle it
  (read-controller player player-controller time-delta)

  ;; Update player velocity from acceleration
  (let ((vel-x (min (+ (player-vel-x player)
                       (* (player-accel-x player) time-delta))
                    player-max-vel-x)))
    (player-vel-x-set! player (/ vel-x (+ 1 (* damping time-delta)))))
  (player-x-set! player (+ (player-x player)
                           (player-vel-x player)))

  ;; Update player position based on velocity
  (if (equal? (player-state player) 'jumping)
      (begin
        (player-vel-y-set! player (min (+ (player-vel-y player)
                                          (* gravity-accel-y time-delta))
                                       player-max-vel-y))
        (player-y-set! player (+ (player-y player)
                                 (player-vel-y player)))))

  ;; When checking for falling, it makes sense to have the bottom-y be at the
  ;; full extent of the player so that we always find the floor under their feet

  (let ((left-x (floor (/ (player-x player)
                          tile-size)))
        (right-x (floor (/ (+ (player-x player)
                              (- player-size 1))
                           tile-size)))
        (top-y (floor (/ (player-y player)
                         tile-size)))
        (bottom-y (floor (/ (+ (player-y player)
                               player-size)
                            tile-size)))
        (vel-x (player-vel-x player))
        (vel-y (player-vel-y player)))

    (check-falling-collision screen player left-x right-x top-y bottom-y)
    (check-moving-collision screen player left-x right-x vel-x)

    ;; Check for collision against a portal
    (let ((portal (find-activated-portal screen
                                         left-x right-x
                                         top-y bottom-y
                                         vel-x vel-y)))
      (if portal (game-event-handler (cons 'change-screen portal))))))

(define (find-activated-portal screen left-x right-x top-y bottom-y vel-x vel-y)
  ;; TODO: We do this weird if check because the Y axis checks were not working
  ;; reliably before!
  (if (not (equal? vel-y 0))
      (or (and (< vel-y 0)
               (or (screen-portal-at-pos screen
                                         left-x
                                         top-y
                                         portal-direction-up)
                   (screen-portal-at-pos screen
                                         right-x
                                         top-y
                                         portal-direction-up)))
          (and (> vel-y 0)
               (or (screen-portal-at-pos screen
                                         left-x
                                         bottom-y
                                         portal-direction-down)
                   (screen-portal-at-pos screen
                                         right-x
                                         bottom-y
                                         portal-direction-down))))
      (or (and (< vel-x 0)
               (or (screen-portal-at-pos screen
                                         left-x
                                         top-y
                                         portal-direction-left)
                   (screen-portal-at-pos screen
                                         left-x
                                         bottom-y
                                         portal-direction-left)))
          (and (> vel-x 0)
               (or (screen-portal-at-pos screen
                                         right-x
                                         top-y
                                         portal-direction-right)
                   (screen-portal-at-pos screen
                                         right-x
                                         bottom-y
                                         portal-direction-right))))))

(define (render-player renderer player window-width window-height) :export
  (renderer-draw-rect renderer
                      (player-x player)
                      (player-y player)
                      player-size
                      player-size
                      player-color))
